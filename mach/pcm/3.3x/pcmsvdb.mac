
nmijum:	jp	ste8
brkjum:	jp	brrout
jrst38	equ	38h
jnmi	equ	66h
hwsp	equ	43h
debug::	ld	sp,stapze
	call	clsta
	call	agzk
	db	0ch,'Debugger V.'
	VERS
	db	0dh,8ah
dinist:	ld	sp,stapze
	xor	a
	ld	(escmark),a
	call	csuch
	jr	dinist
lee8:	call	lee4
lee4:	call	lee2
lee2:	call	lee1
lee1:	ld	c,020h
lee0:	jp	conout
wrzv:	call	agzk	
	db	00dh,08ah
	ret	
hzl:	ld	hl,(aaaa)
	ld	de,(bbbb)
	ld	bc,(cccc)
	ret	
ip1:	db	0,9,'W',0dh
ipc:	ld	hl,(rpc)
	inc	hl
ipc1:	ld	(rpc),hl
ipc2:	jp	anze1
dp1:	db	0,9,'Z',0dh
dpc:	ld	hl,(rpc)
	dec	hl
	jr	ipc1
iblo:	db	0,9,'U',0dh
iblo1:	ld	hl,(rpc)
	ld	de,00080h
	jr	dblo2
dblo:	db	0,9,'O',0dh
dblo1:	ld	hl,(rpc)
	ld	de,0ff80h
dblo2:	add	hl,de
	jr	ipc1
setm:	db	0,9,'N',0dh
setm1:	call	hzl
	ld	a,l
	ld	hl,(rpc)
	ld	(hl),a
	cp	(hl)
	jp	nz,derror
	jr	ipc2
step:	db	0,9,'Y',0dh
step1:	call	ste1		
	jp	csuch
degl:	push	af
	push	hl
	call	agzk
domerk:	db	' (R',')'+80h	
degl1:	call	conin		
	ld	c,a		
	push	af
	call	conout		
	pop	af
	cp	0dh		
	jr	nz,degl1	
	call	wrzv		
	ld	hl,bsanf+(bline*bwidth)-1
	ld	bc,bline*bwidth-1
	ld	a,')'
degl2:	cpd
	jr	nz,degl2
	inc	hl
	inc	hl
	ld	(zanf),hl
	pop	hl
	pop	af
	ret
lzeio:	ld	hl,bsanf+(bline*bwidth)-1
lzei1:	dec	hl
	ld	a,(hl)
	res	7,a
	cp	020h
	jr	z,lzei1
	inc	hl
gkp:	dec	hl
	ld	a,(hl)
	jp	grkl
retreg:	ld	(zws2),sp
	ld	sp,rsp
mar:	push	iy
	push	ix
	push	hl
	push	de
	push	bc
	push	af
	exx
	ex	af,af'
	push	hl
	push	de
	push	bc
	push	af
	jr	retu
rucreg:	ld	(zws2),sp
	ld	sp,rafs
	pop	af
	pop	bc
	pop	de
	pop	hl
	exx
	ex	af,af'
	pop	af
	pop	bc
	pop	de
	pop	hl
	pop	ix
	pop	iy
retu:	ld	sp,(zws2)
	ret	
nfund:	call	derror		
csuch:	call	degl		
	ld	bc,lanvdb	
	ld	hl,ip1		
such:	xor	a		
	cpir
	jr	nz,nfund	
	ld	a,9		
	cp	(hl)
	jr	nz,such		
	inc	hl		
	ld	de,(zanf)	
cs3:	call	grklo		
	cp	(hl)		
	jr	nz,such		
	inc	hl		
	inc	de		
m1:	ld	a,(hl)		
	cp	00dh		
	jr	nz,such		
	inc	hl		
	push	hl		
	push	hl
	call	lzeio		
	cp	':'		
	pop	hl
	jr	z,neintr	
	inc	de		
	call	asbin		
eintr:	ld	(aaaa),hl	
	call	asbin		
	ld	(bbbb),hl	
	call	asbin		
	ld	(cccc),hl	
neintr:	pop	hl		
	ld	bc,csuch	
	push	bc		
	jp	(hl)		
spacet:	db	0,9,' ',0dh
	ret	
rsprun:	db	0,9,'Q',0dh
	jp	begvt
initia:	db	0,9,'I',0dh
	call	agzk
	db	'Clear!',0dh,8ah
clsta:	ld	hl,aaaa
	ld	b,032h
clsta1:	ld	(hl),000h
	inc	hl
	djnz	clsta1
	ld	a,001h
	ld	(astep),a
	ld	a,hwsp
	ld	(rsp+01h),a
	ret
du1:	ld	hl,(aaaa)
du2:	ld	de,(bbbb)
	push	hl
	or	a
	sbc	hl,de
	pop	hl
	ret	nc
	call	wrzv
	push	hl
	ld	a,l
	and	0f0h
	ld	l,a
	ld	de,(rpc)
	ld	a,e
	and	0f0h
	ld	e,a
	sbc	hl,de
	pop	hl
	jr	nz,du4
	push	hl
	ld	hl,(rpc)
	call	zahex
	pop	hl
	call	agzk
	db	02dh,0beh	
	jr	du5
du4:	call	zahex
	call	lee2
du5:	ld	b,010h
du6:	push	hl
	ex	de,hl
	ld	hl,(rpc)
	or	a
	sbc	hl,de
	jr	nz,du8
	call	agzk
	db	0beh	
	pop	hl
	jr	du9
du8:	pop	hl
	call	lee1
du9:	ld	a,b
	and	003h
	call	z,lee1
	ld	a,(hl)
	call	ahex
	inc	hl
	djnz	du6
	jr	du2
modify:	db	0,9,'M',0dh
mofi1:	call	hzl		
mofi2:	push	hl		
	call	zahex		
	call	lee2		
	ld	a,(hl)		
	call	ahex		
	call	degl		
	pop	hl		
	ld	de,(zanf)	
	dec	hl		
mofi3:	inc	hl		
	push	hl
	call	asbin		
	ld	a,l
	or	a
	jr	z,max		
mofi4:	pop	hl
	ld	(hl),a		
	cp	(hl)		
	jr	z,mofi3		
	call	derror		
	call	lee1		
	jr	mofi2		
max:	ld	a,(de)		
	cp	020h
	ld	a,0
	jr	z,mofi4  	
	ld	a,(de)		
	pop	hl
	inc	hl		
	ld	(bbbb),hl	
	cp	'.'		
	jp	z,csuch		
	ld	de,(zanf)	
	cp	020h		
	jr	z,mofi2		
	dec	hl
	jr	mofi2
patter:	db	0,9,'P',0dh
patt1:	call	hzl
	xor	a
	ex	de,hl
	sbc	hl,de
	jp	z,derror
	jp	c,derror
	ex	de,hl
	ld	(hl),c
	push	de
	pop	bc
	push	hl
	pop	de
	inc	de
pa1:	ldir
	ret	
transf:	db	0,9,'T',0dh
trans1:	call	hzl
	xor	a
	push	hl
	sbc	hl,de
	pop	hl
	jp	nc,pa1
	add	hl,bc
	ex	de,hl
	add	hl,bc
	ex	de,hl
	dec	hl
	dec	de
	lddr
	ret	
sequ:	ld	b,004h
sequ1:	ld	a,(hl)
	call	ahex
	inc	hl
	call	lee1
	djnz	sequ1
	ret	
rega:	call	agzk
	db	'AF',0bah	
	call	raga
ra3:	call	agzk
	db	'BC',0bah	
	call	raga
	call	agzk
	db	'DE',0bah	
	call	raga
	call	agzk
	db	'HL',0bah	
	jr	raga
inhl:	call	agzk
	db	'(HL)',0bah	
	ld	a,(hl)
	call	ahex
	jp	wrzv
raga:	call	lee1
	ld	a,(hl)
	push	af
	inc	hl
	ld	a,(hl)
	inc	hl
	call	ahex
	call	lee1
	pop	af
	call	ahex
	jp	lee2
regis:	call	agzk
	db	'MAIN ',0a0h	
	ld	hl,raf
	call	rega
	ld	hl,(rhl)
	call	inhl
	call	agzk
	db	'EXXR ',0a0h	
	ld	hl,rafs
	call	rega
	ld	hl,(rhls)
	jr	inhl
anza:	call	zahex
	jp	lee4
regdis:	db	0,9,'R',0dh
rdis0:	call	lzeio
	cp	':'
	jp	nz,reganz
rdis2:	call	regis
	call	agzk
	db	'FLAG ',0a0h	
	ld	a,(raf)
	call	sfl
	call	agzk
	db	'  ',0a8h
	ld	a,(rafs)
	call	sfl
	call	agzk
	db	')',0dh,0ah
	db	'IX',0bah	
	ld	hl,(rix)
	call	zahex
	call	lee4
	call	agzk
	db	'IY',0bah	
	ld	hl,(riy)
	call	zahex
	call	lee8
	call	lee2
	call	agzk
	db	'I ',0bah	
	ld	a,(ri)
	call	ahex
	call	agzk
	db	'     SR',0bah		
	ld	hl,(astep)
	call	zahex
	call	agzk
	db	0dh,0ah,'SP',0bah	
	ld	hl,(rsp)
	call	zahex
	call	lee4
	call	sequ
	call	agzk
	db	'     BP',0bah		
	ld	hl,(rbp)
	call	zahex
	call	lee4
	call	sequ
	call	wrzv
	call	anzpc
	jp	wrzv
anzpc:	call	agzk
	db	'PC',0bah	
	ld	hl,(rpc)
	jp	zahex
sfl:	ld	l,a
	bit	7,l
	jr	z,zfl
	call	agzk
	db	0d3h	
zfl:	bit	6,l
	jr	z,hfl
	call	agzk
	db	0dah	
hfl:	bit	4,l
	jr	z,pfl
	call	agzk
	db	0c8h	
pfl:	bit	2,l
	jr	z,nfl
	call	agzk
	db	0d0h	
nfl:	bit	1,l
	jr	z,cfl
	call	agzk
	db	0ceh	
cfl:	bit	0,l
	ret	z
	call	agzk
	db	0c3h	
	ret	
reganz:	ld	hl,(zanf)
	inc	hl
regan1:	inc	hl
	ld	a,(hl)
	cp	020h
	jr	nz,regan1
	push	hl
	call	gkp
	ld	de,raf
	cp	27h
	jr	nz,r1
	ld	de,rafs
	call	gkp
	cp	'M'
	jp	z,manzs
r1:	cp	'M'
	jr	z,manz
	cp	'F'
	jr	z,mr1
	inc	de
	inc	de
	cp	'C'
	jr	nz,r2
	call	gkp
	cp	'P'
	jr	nz,mr1
	ld	de,rpc
	jr	mr1
r2:	inc	de
	inc	de
	cp	'E'
	jr	z,mr1
	inc	de
	inc	de
	cp	'L'
	jr	z,mr1
	ld	de,rix
	cp	'X'
	jr	z,mr1
	inc	de
	inc	de
	cp	'Y'
	jr	z,mr1
	inc	de
	inc	de
	cp	'P'
	jr	z,mr1
	inc	de
	inc	de
	cp	'R'
	jr	z,mr1
	inc	de
	inc	de
	cp	'I'
	jp	nz,fin6
mr1:	pop	hl
	push	de
	ex	de,hl
	call	asbin
	pop	de
	ex	de,hl
	ld	(hl),e
	inc	hl
	ld	(hl),d
	jp	anze1
manz:	ld	hl,(rhl)
	jr	mahl
manzs:	ld	hl,(rhls)
mahl:	pop	de
	push	hl
	call	asbin
	ex	de,hl
	pop	hl
	ld	(hl),e
	jp	anze1
brrout:	call	retreg
	pop	hl
	dec	hl
	ld	(rsp),sp
	ld	sp,stapze
	call	agzk
	db	'Break at',0bah	
	ld	(rpc),hl
	ld	hl,(rbp)
	ld	de,rbps
	ld	a,(de)
	ld	(hl),a
	call	anza
	ld	sp,stapze
	jp	csuch
bpoint:	db	0,9,'B',0dh
bpn1:	call	hzl
	ld	(rbp),hl
	ld	de,rbps
	ld	a,(hl)
	ld	(de),a
	ld	a,0c3h
	ld	(jrst38),a
	ld	hl,brkjum
	ld	(jrst38+01h),hl
	jp	anze1
exec:	db	0,9,'E',0dh	
brka:	ld	hl,(rbp)	
	ld	(hl),0ffh
jump:	db	0,9,'J',0dh
jum1:	ld	b,10		
	ld	hl,(rsp)	
jum2:	dec	hl		
	ld	a,(hl)		
	cpl			
	ld	(hl),a		
	cp	(hl)		
	jr	nz,go2		
	cpl			
	ld	(hl),a		
	djnz	jum2		
	ld	hl,(aaaa)
	ld	(rpc),hl
	ld	sp,(rsp)
	push	hl
	jp	rucreg
go:	db	0,9,'G',0dh	
go23:	ld	hl,(aaaa)	
	jp	(hl)
gbrk:	db	0,9,'F',0dh	
go1:	ld	hl,(rpc)	
	ld	(aaaa),hl
	ld	de,(rbp)
	xor	a
	sbc	hl,de
	jr	nz,brka
	jr	jum1
go2:	call	agzk		
	db	'Init SP',0a1h
	ret	
comp:	db	0,9,'C',0dh	
com1:	call	hzl		
com2:	ld	a,(de)		
	cp	(hl)		
	jr	nz,com4		
com3:	dec	bc		
	inc	hl		
	inc	de
	ld	a,b		
	or	c
	ret	z		
	jr	com2		
com4:	call	anza		
	ld	a,(hl)		
	call	ahex
	call	lee8		
	ex	de,hl
	call	anza
	ex	de,hl
	ld	a,(de)
	call	ahex
	call	wrzv
	call	conin		
	cp	0dh		
	ret	nz		
	jr	com3
finda:	db	0,9,'A',0dh
fina1:	call	hzl		
fina2:	ld	de,(zanf)	
	inc	de		
	inc	de
fina5:	ld	a,(de)		
	cp	(hl)		
	jr	z,fina3
	inc	hl		
fina4:	ld	a,h		
	or	l
	jr	z,fin6		
	jr	fina2		
fina3:	ld	(aaaa),hl	
fina7:	inc	hl		
	inc	de
	ld	a,(de)		
	cp	020h		
	jr	z,finanz	
	cp	(hl)		
	jr	nz,fina2	
	jr	fina3		
finan0:	push	bc
	pop	hl
finanz:	ld	(rpc),hl	
	ld	(aaaa),hl	
	jp	mofi1		
find:	db	0,9,'H',0dh
	call	hzl		
	push	hl
	pop	bc
finh1:	ld	de,(zanf)	
	inc	de		
	inc	de
finh2:	call	asbin		
finh3:	ld	a,(bc)
	cp	l		
	jr	z,finh4
	inc	bc		
	ld	a,b
	or	c		
	jr	z,fin6		
	jr	finh3		
finh4:	inc	bc		
	ld	a,b
	or	c		
	jr	z,fin6		
	call	asbin		
	ld	a,(de)
	cp	020h		
	jr	z,finan0	
	ld	a,(bc)
	cp	l		
	jr	nz,finh1	
	jr	finh4		
fin6:	call	agzk
	db	'Not found!',0dh,8ah
	jp	dinist	
anze:	db	0,9,'K',0dh
anze1:	call	agzk
	db	8ch
	call	rdis2
	ld	hl,(rpc)
	ld	a,l
	and	080h
	ld	l,a
	ld	(aaaa),hl
	or	07fh
	ld	l,a
	ld	a,0ffh
	cp	h
	jr	nz,anze4
	bit	7,l
	jr	z,anze4
	ld	hl,0ffefh
anze4:	ld	(bbbb),hl
	call	du1
	jp	wrzv
pi:	db	0,9,'<',0dh	
pi1:	call	hzl		
	ld	c,l		
	ld	b,h
	in	a,(c)		
	push	af
	call	lee2		
	pop	af
	call	ahex		
	jp	wrzv		
	db	0,9,'>',0dh	
po1:	call	hzl		
	ld	b,h		
	ld	c,l		
	out	(c),e		
	ret			
ste1:	call	wrzv
ste2:	ld	hl,(astep)
	ld	(zws1),hl
ste5:	ld	hl,nmijum
	ld	(jnmi+01h),hl
	ld	a,0c3h
	ld	(jnmi),a
	call	rucreg
	ld	sp,(rsp)
	push	hl
	ld	hl,(rpc)
	ex	(sp),hl
	push	af
	ld	a,(ri)
	ld	i,a
stport:	out	(outnmi),a
	pop	af
	ret	
ste8:	di
	push	hl
	ld	hl,ste6
	ex	(sp),hl
	retn
ste6:	ex	(sp),hl
	ld	(rpc),hl
	pop	hl
	ld	(rsp),sp
	ld	sp,stapze
	call	retreg
	ld	hl,(astep)
	dec	hl
	ld	(astep),hl
	ld	a,h
	or	l
	jr	nz,ste5
	ld	hl,(zws1)
	ld	(astep),hl
	call	anze1
rteur:	ld	sp,stapze
	jp	csuch
tasct:	push 	hl
	ld	hl,zme
	ld	(hl),0ffh
	pop	hl
	jr	tasc0
tascs:	call	wrzv
tascb:	push	hl
	ld	hl,zme
	ld	(hl),0
	pop	hl
tasc0:	call	hzl
tasc1:	push	hl
	or	a
	sbc	hl,de
	pop	hl
	ret	nc
	ld	a,(hl)
	and	07fh
	inc	hl
	cp	07fh
	jr	z,tasc3
	cp	020h
	jr	nc,tasc2
tasc3:	push 	hl
	ld	hl,zme
	bit	0,(hl)
	pop	hl
	jr	nz,tasc2
	ld	a,'.'
tasc2:	ld	c,a
	call	conout
	jr	tasc1
tx:	db	0,9,'X',0dh
send:	call	lzeio
	cp	'H'
	jp	z,du1
	cp	'T'
	jr	z,tasct
	cp	'S'
	jr	z,tascs
	jp	fin6
lanvdb	equ	$-ip1		
aaaa:	dw	0
bbbb:	dw	0
cccc:	dw	0
zanf:	dw	0
zme:	ds	1
zws1:	dw	0
zws2:	dw	0
rafs:	dw	0
	dw	0
	dw	0
rhls:	dw	0	
raf:	dw	0
	dw	0
	dw	0
rhl:	dw	0
rix:	dw	0
riy:	dw	0
rsp:	dw	0
astep:	dw	0
ri:	dw	0
rpc:	dw	0
	dw	0
rbp:	ds	2
rbps:	ds	6

